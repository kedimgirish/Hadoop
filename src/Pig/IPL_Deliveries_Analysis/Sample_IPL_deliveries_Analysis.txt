delivers = LOAD 'deliveries.csv' USING PigStorage(',') AS (match_id:int,inning:int,batting_team:chararray,bowling_team:chararray,over:int,ball:int,batsman:chararray,non_striker:chararray,bowler:chararray,is_super_over:int,wide_runs:int,bye_runs:int,legbye_runs:int,noball_runs:int,penalty_runs:int,batsman_runs:int,extra_runs:int,total_runs:int,player_dismissed:chararray,dismissal_kind:chararray,fielder:chararray);

delivers_ranked = RANK delivers;

NoHeader = FILTER delivers_ranked BY (rank_delivers > 1);

Ordered_Delivers = ORDER NoHeader BY rank_delivers;


-- Q) Who scored most number of runs in super over

super_overs  = FILTER Ordered_Delivers BY (is_super_over == 1 AND batsman_runs != 0); -- Filter the superover column and batsmen runs should not be zero

super_overs_runs = FOREACH super_overs GENERATE batsman,total_runs; -- select only required columns

super_overs_runs_group = GROUP super_overs_runs BY batsman; -- Group by batsman name

batsman_total_runs = FOREACH super_overs_runs_group GENERATE group,SUM(super_overs_runs.total_runs) as totalRuns;

batsman_runs_ordered = ORDER batsman_total_runs BY totalRuns DESC;

DUMP batsman_runs_ordered;

-- (CH Gayle,25)


-- Q) Who scored most number of runs overall

batsmen_runs_filtered = FILTER Ordered_Delivers BY batsman_runs != 0; -- select the columns where batsman_runs column is not zero

batsmen_runs = FOREACH batsmen_runs_filtered GENERATE batsman,total_runs;

batsmen_grouped = GROUP batsmen_runs BY batsman;

batsmen_total_runs = FOREACH batsmen_grouped GENERATE group,SUM(batsmen_runs.total_runs) as totalRuns;

batsmen_runs_ordered = ORDER batsmen_total_runs BY totalRuns DESC; -- put the data into desc order

top5_data = LIMIT batsmen_runs_ordered 5;

DUMP top5_data; -- Top 5 records


-- (SK Raina,4557)
-- (V Kohli,4429)


-- Q) Which batsmen got out most number of times to which bowler

-- First filter the dismissals

dismissals = FILTER Ordered_Delivers BY (dismissal_kind == 'caught' OR dismissal_kind == 'bowled' OR dismissal_kind == 'caught and bowled' OR dismissal_kind == 'hit wicket' OR dismissal_kind == 'lbw' OR dismissal_kind == 'stumped');

dismissals_columns = FOREACH dismissals GENERATE batsman,bowler,dismissal_kind;

dismisals_group = GROUP dismissals_columns BY (batsman,bowler);

dismissals_count = FOREACH dismisals_group GENERATE group,COUNT(dismissals_columns.dismissal_kind) as dismissalCount;

dismissals_count_ordered = ORDER dismissals_count BY dismissalCount DESC;

DUMP dismissals_count_ordered;

-- ((MS Dhoni,Z Khan),7)
-- ((G Gambhir,Z Khan),6)


-- Q) What kind of dismissals are more? is it LBW or Catch or Stumped?

dismissals = FILTER Ordered_Delivers BY (dismissal_kind IS NOT NULL);

dismissals_type = FOREACH dismissals GENERATE dismissal_kind;

dismissals_type_group = GROUP dismissals_type BY dismissal_kind;

--dismissals_type_group: {group: chararray,dismissals_type: {(dismissal_kind: chararray)}}

dismissals_type_group_count = FOREACH dismissals_type_group GENERATE group, COUNT(dismissals_type.dismissal_kind);

DUMP dismissals_type_group_count;